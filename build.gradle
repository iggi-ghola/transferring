group 'com.kozhekin'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = "com.kozhekin.transferring.Launcher"

repositories {
    mavenCentral()
}
ext {
    undertow_version = '2.0.29.Final'
    slf4j_version = '1.7.30'
    logback_version = '1.2.3'
    resteasy_version = '3.9.0.Final'
}

dependencies {
    implementation "io.undertow:undertow-core:$undertow_version"
    implementation "io.undertow:undertow-servlet:$undertow_version"

    implementation "org.jboss.resteasy:resteasy-jaxrs:$resteasy_version"
    implementation "org.jboss.resteasy:resteasy-jackson-provider:$resteasy_version"
    implementation "org.jboss.resteasy:resteasy-undertow:$resteasy_version"

    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: logback_version

//    implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'

    testImplementation "org.jboss.resteasy:resteasy-client:$resteasy_version"
    testImplementation group: 'junit', name: 'junit', version: '4.13'
}

test {
    useJUnit {
        excludeCategories(
                'com.kozhekin.transferring.category.SlowTest',
                'com.kozhekin.transferring.category.StressTest')
    }
}

task slowTests(type: Test) {
    useJUnit {
        includeCategories('com.kozhekin.transferring.category.SlowTest')
    }
}

task allTests(type: Test) {
    useJUnit {
        excludeCategories('com.kozhekin.transferring.category.StressTest')
    }
}

task stressTests(type: Test) {
    systemProperty "stress.time.minutes", System.getProperty("stress.time.minutes")
    useJUnit {
        includeCategories('com.kozhekin.transferring.category.StressTest')
    }
}
